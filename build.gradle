plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'org.kinotic'
version '3.9.19'

sourceCompatibility = '11'

repositories {
    mavenCentral()
}

ext {
    apacheCommonsVersion='3.14.0'
    logbackVersion='1.2.13'
    nettyVersion='4.1.101.Final'
    vertxVersion='3.9.16'
}


dependencies {
    implementation "io.vertx:vertx-core:${vertxVersion}"
    implementation "io.vertx:vertx-web:${vertxVersion}"

    // We override netty to use the latest version, allowing us to patch security issues
    implementation "io.netty:netty-buffer:${nettyVersion}"
    implementation "io.netty:netty-codec-http2:${nettyVersion}"
    implementation "io.netty:netty-codec-http:${nettyVersion}"
    implementation "io.netty:netty-common:${nettyVersion}"
    implementation "io.netty:netty-handler-proxy:${nettyVersion}"
    implementation "io.netty:netty-handler:${nettyVersion}"
    implementation "io.netty:netty-resolver-dns:${nettyVersion}"
    implementation "io.netty:netty-resolver:${nettyVersion}"
    implementation "io.netty:netty-transport:${nettyVersion}"

    implementation "ch.qos.logback:logback-core:${logbackVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"

    implementation "org.apache.commons:commons-lang3:${apacheCommonsVersion}"

    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            from components.java

            pom {
                name = 'Vert.x STOMP Server'
                description = 'A simplified STOMP server for use with Vert.x'
                url = 'https://github.com/Kinotic-Foundation/vertx-stomp-lite'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Kinotic Developers'
                        email = 'develop@kinotic.org'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/Kinotic-Foundation/vertx-stomp-lite.git'
                    developerConnection = 'scm:git:https://github.com/Kinotic-Foundation/vertx-stomp-lite.git'
                    url = 'https://github.com/Kinotic-Foundation/vertx-stomp-lite'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv('MAVEN_USERNAME')
                password = System.getenv('MAVEN_PASSWORD')
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
